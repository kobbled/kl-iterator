PROGRAM class_name
%NOBUSYLAMP
%COMMENT = 'iterator class'
%NOLOCKGROUP
%NOPAUSE = COMMAND + TPENABLE + ERROR

%defeval BASE_CLASSNAME class_name

%ifdef ITER_STRUCT
--define custom struct if supplied
ITER_STRUCT(class_name)
%endif

%ifdef ITER_STRUCT_IMPORT
--import custom struct if specified
%mode push
%mode quote "$"
%eval $%include ITER_STRUCT_IMPORT$
%mode pop
%endif

%include systemvars.klt
%include systemlib.macros.klt
--include atomic types
%include systemlib.types.klt

%include errors.klt

%ifdef ENABLE_REGMAPPING
--regmap getters/setters
%include regmap.klt
%endif

%defeval class_name BASE_CLASSNAME

CONST
  --tp functions
  ITER_RESET  = 1
  ITER_PUSH   = 2
  ITER_POP    = 3
  ITER_INSERT = 4
  ITER_GET    = 5
  ITER_NEXT   = 6
  ITER_PREV   = 7
  ITER_NULL   = 8

VAR
  this : PATH nodedata = ITR_TYPE
  nde :  ITR_TYPE
  index : INTEGER
  --tp variables
  ITER_TP_VARS


--includes
%include errors.klh

%ifdef ENABLE_REGMAPPING
--imports for tp interfacing
%from registers.klh %import get_int, get_real, get_string, get_io, get_type_enum, set_boolean
%include registerstp.klh
%endif

%from pose.klh %import get_posreg_joint, get_posreg_xyz
%from tpe.klh %import get_int_arg, get_real_arg, get_string_arg, get_boolean_arg

--include template specific imports
define_itr_private_headers

%defeval class_name BASE_CLASSNAME

-- iterator class members
%include iteratorpath.klh

-- **** DEFINED TEMPLATES
  -- private template members
define_null_type(class_name)
  -- public template members
define_is_null(class_name)

%ifdef define_unwrap_pop
define_unwrap_pop(class_name)
%endif
%ifdef define_unwrap_next
define_unwrap_next(class_name)
%endif
%ifdef define_unwrap_prev
define_unwrap_prev(class_name)
%endif
%ifdef define_unwrap_from
define_unwrap_from(class_name)
%endif
%ifdef define_unwrap
define_unwrap(class_name)
%endif
%ifdef define_wrap
define_wrap(class_name)
%endif
%ifdef define_wrap_insert
define_wrap_insert(parent)
%endif

--private

ROUTINE clear_
  VAR
    i : INTEGER
    status : INTEGER
  BEGIN
    i = PATH_LEN(this)
    WHILE PATH_LEN(this) > 0 DO
		  DELETE_NODE(this,i,status)
      CHK_STAT(status)
      i = i - 1
	  ENDWHILE
  END clear_

-- public

ROUTINE new
  BEGIN
    index = 1
    --clear members
    clear_
    --uninitailize
    nde = set_null
  END new

ROUTINE delete
  BEGIN
    index = 0
    --clear members
    clear_
    --uninitailize
    nde = set_null
  END delete

ROUTINE next
  BEGIN
    IF index > PATH_LEN(this) THEN
      RETURN(set_null)
    ENDIF

    nde = this[index]
    index = index + 1
    RETURN(nde)
  END next

ROUTINE prev
  BEGIN
    IF (index) < 1 THEN
      RETURN(set_null)
    ENDIF

    nde = this[index]
    index = index - 1
    RETURN(nde)
  END prev

ROUTINE push
  VAR
    status : INTEGER
  BEGIN
    --add to
    APPEND_NODE(this, status)
    CHK_STAT(status)
    this[PATH_LEN(this)] = n

    IF PATH_LEN(this) = 1 THEN
      nde = this[1]
    ENDIF
  END push

ROUTINE pop
  VAR
    status : INTEGER
  BEGIN
    IF (PATH_LEN(this) = 0) THEN
      nde = set_null
      RETURN(nde)
    ENDIF

    nde = this[PATH_LEN(this)]
    --add to
    DELETE_NODE(this, PATH_LEN(this), status)
    CHK_STAT(status)

    RETURN(nde)
  END pop

ROUTINE insert
  VAR
    status : INTEGER
  BEGIN
    --add to
    INSERT_NODE(this, index_, status)
    CHK_STAT(status)
    this[index_] = n
  END insert

ROUTINE take
  VAR
    status : INTEGER
  BEGIN
    IF (index + i) > PATH_LEN(this) THEN
      karelError(PATH_INDEX_OUT_OF_RANGE,'take is out of range on iterator.', ER_ABORT)
    ENDIF

    COPY_PATH(this, index, (index+i), out_pth, status)
    CHK_STAT(status)
  END take

ROUTINE skip
  BEGIN
    IF (index + i) > PATH_LEN(this) THEN
      karelError(PATH_INDEX_OUT_OF_RANGE,'skip is out of range on iterator.', ER_ABORT)
    ENDIF

    index = index + i
    nde = this[index]
    RETURN(nde)
  END skip

ROUTINE set_index
  BEGIN
    IF (i = 0) THEN
      karelError(PATH_INDEX_OUT_OF_RANGE,'paths start at index 1', ER_ABORT)
    ENDIF
    IF (i > PATH_LEN(this)) THEN
      karelError(PATH_INDEX_OUT_OF_RANGE,'set index outside of range', ER_ABORT)
    ENDIF
    --handle reverse
    IF (i < 0) THEN
      i = PATH_LEN(this) + (i+1)
      IF i < 1 THEN
        karelError(PATH_INDEX_OUT_OF_RANGE,'reverse index out of range. Using first index.', ER_WARN)
        i = 1
      ENDIF
    ENDIF

    index = i

    nde = this[index]
  END set_index

ROUTINE get_index
  BEGIN
    IF (i = 0) THEN
      karelError(PATH_INDEX_OUT_OF_RANGE,'paths start at index 1', ER_ABORT)
    ENDIF
    IF (i > PATH_LEN(this)) THEN
      karelError(PATH_INDEX_OUT_OF_RANGE,'get index outside of range', ER_ABORT)
    ENDIF
    --handle reverse
    IF (i < 0) THEN
      i = PATH_LEN(this) + (i+1)
      IF i < 1 THEN
        karelError(PATH_INDEX_OUT_OF_RANGE,'reverse index out of range. Using first index.', ER_WARN)
        i = 1
      ENDIF
    ENDIF
    RETURN(this[i])
  END get_index

ROUTINE get
  BEGIN
    RETURN(nde)
  END get

ROUTINE is_empty
  BEGIN
    RETURN(PATH_LEN(this) = 0)
  END is_empty

ROUTINE len
  BEGIN
    RETURN(PATH_LEN(this))
  END len

BEGIN
  ITER_TP_INTERFACE
END class_name
